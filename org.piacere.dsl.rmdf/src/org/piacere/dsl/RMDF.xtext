/**
 * Resource Model Definition
 *
 * Author: Tasio Mendez (Politecnico di Milano)
 * URL: https://github.com/tasiomendez/
 * Version: 1.0
 */

grammar org.piacere.dsl.RMDF with org.eclipse.xtext.common.Terminals

generate rMDF "http://www.piacere.org/dsl/RMDF"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RMDFModel:
	(elements += AbstractElement)*
;

AbstractElement:
	('metadata:' 
		BEGIN
			metadata=CMetadata
		END
	) &
	('imports:'
		BEGIN
			imports=CImports
		END
	)? &
	('data_types:'
		BEGIN 
			datatypes=CDataTypes
		END
	)? & 
	('node_types:' 
		BEGIN
			nodetypes=CNodeTypes
		END
	)
;

// METADATA Block
// ---

CMetadata:
	('_provider:' provider=ID) &
	('_version:' version=CSTRING) &
	('_description:' description=CSTRING)?
;

// IMPORTS Block
// ---

CImports:
	(imports+=CImport)+
;

CImport:
	'-' importedName=QualifiedNameWithWildcard
;

// DATATYPES Block
// ---
		
CDataTypes:
	(datatypes+=CDataType)+
;

CDataType:
	name=CQualifiedName ':' 
	BEGIN
		data=CDataTypeData
	END
;

CDataTypeData:
	('description:' description=CSTRING)? &
	('properties:' 
		BEGIN
			properties+=CProperty+
		END
	)
;

// NODETYPES Block
// ---

CNodeTypes:
	(nodetypes+=CNodeDescription)+
;

CNodeDescription:
	CNodeType
;

CNodeType:
	name=CQualifiedName ':'
	BEGIN 
		data=CNodeTypeData
	END
;

CNodeTypeData:
	('description:' description=CSTRING) &
	('properties:'
		BEGIN 
			(properties+=CProperty)+
		END
	)? &
	('node_templates:' 
		BEGIN
			nodes=CNodeTemplates
		END
	)?
;

// Properties Definition
// ---

CProperty:
	(name=ID ':'
		BEGIN 
			property=CPropertyBody
		END
	)
;

CPropertyBody:
	('type:' type=CValueData) &
	('default:' default=CValueExpression)? &
	('description:' description=CSTRING)? &
	('required:' required=CBOOLEAN)? & 
	('multiple:' multiple=CBOOLEAN)?
;

CValueData:
	predefined=CValueDataPredefined | datatype=[CDataType|CQualifiedName]
;

// Node Templates Definition
// ---

CNodeTemplates:
	(nodes+=CNodeTemplate)+
;

CNodeTemplate:
	name=ID ':'
	BEGIN
		template=CNode
	END
;

CNode:
	('type:' type=[CNodeDescription|CQualifiedName] ) &
	('properties:' 
		BEGIN
			(properties+=CNodeProperty)+
		END
	)? &
	('relationships:' 
		BEGIN
			relationships=CNodeRelationships
		END
	)? 
;

// Node Properties

CNodeProperty:
	name=[CProperty|CQualifiedName] ':' value=CNodePropertyValue
;

CNodePropertyValue:
	CNodePropertyValueInline | CNodeNestedProperty
;

CNodePropertyValueInline:
	CNodePropertyValueInlineSingle | CMultipleValueExpression
;

CNodePropertyValueInlineSingle:
	CValueExpression | CNodePropertyValueCrossRef
;

CNodeNestedProperty:
	 BEGIN
	 	properties+=(CNodeProperty)+
	 END
;

CMultipleValueExpression:
	BEGIN
		('-' values+=(CMultipleNestedProperty | CNodePropertyValueInlineSingle))+
	END
;

CMultipleNestedProperty:
	(first=CNodeProperty)
	(rest=CNodeNestedProperty)?
;

CNodePropertyValueCrossRef:
	CNodeCrossRefGetValue | CNodeCrossRefGetAttribute
;

CNodeCrossRefGetValue:
	"{{" "get_value:" crossvalue=[CProperty|CQualifiedName] "}}"
;

CNodeCrossRefGetAttribute:
	"{{" "get_attribute:" node=[CNodeTemplate|ID] '.' attr=CQualifiedName "}}"
;

// Node Relationships

CNodeRelationships:
	(relationships+=CNodeRelationship)+
;

CNodeRelationship:
	name=ID ':' value=[CNodeTemplate|ID]
;

// Terminal & Custom types
// ---

CQualifiedName:
	ID ('.' ID)*
;

QualifiedNameWithWildcard:
	CQualifiedName '.*'?
;

CValueDataPredefined:
	'String' | 'Integer' | 'Boolean'
;

CValueExpression:
	CSTRING | CFLOAT | CBOOLEAN | CSIGNEDINT
;

CSTRING:
	value = STRING
;

CFLOAT: 
	value = FLOAT	
;

CBOOLEAN: 
	value = BOOLEAN
;

CSIGNEDINT:
	value = SIGNEDINT
;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';
	
SIGNEDINT returns ecore::EInt: '-'? INT;

FLOAT returns ecore::EFloat: INT ('.' INT)?;

// Block with indentation
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';
