/**
 * DevSecOps Modelling Language
 *
 * Author: Tasio Mendez (Politecnico di Milano)
 * URL: https://github.com/tasiomendez/
 * Version: 1.0
 */
 
//grammar org.piacere.dsl.DOML with org.eclipse.xtext.common.Terminals
grammar org.piacere.dsl.DOML with org.piacere.dsl.RMDF

generate dOML "http://www.piacere.org/dsl/DOML"
import "http://www.piacere.org/dsl/RMDF" as rmdf

DOMLModel:
	(elements += AbstractElement)*
;

@Override 
AbstractElement:
	('metadata:'
		BEGIN 
			metadata=CMetadata
		END
	) &
	('input:'
		BEGIN 
			input=CInputVariables
		END
	)? &
	('node_templates:' 
		BEGIN
			nodes=CNodeTemplates
		END
	)? & 
	('node_definition:'
		BEGIN
			definitions=CNodeDefinitions
		END
	)?
;

// METADATA Block
// ---

@Override 
CMetadata:
	('_version:' version=CSTRING) &
	('_description:' description=CSTRING)?
;

// INPUT Variables Block
// ---

CInputVariables:
	(input+=CInputVariable)+
;

CInputVariable:
	name=CQualifiedName ':' 
	BEGIN
		data=CPropertyBody
	END
;

// NODE DEFINITIONS Block

CNodeDefinitions:
	(nodes+=CNodeDefinition)+
;

CNodeDefinition:
	name=CQualifiedName ':'
	BEGIN
		definition=CNodeMapping
	END
;

CNodeMapping:
	('description:' description=CSTRING ) &
	('providers:' 
		BEGIN
			(providers+=CNodeProvider)+
		END
	)?
;

CNodeProvider:
	name=ID ':' provider=[rmdf::CNodeType|CQualifiedName] 
;

// NODE TEMPLATES Block
// Override CNode

@Override 
CNode:
	('type:' ctype=[rmdf::CNodeType|CQualifiedName] | ctype=[CNodeDefinition|CQualifiedName] ) &
	('properties:' 
		BEGIN
			(properties+=CNodeProperty)+
		END
	)? &
	('relationships:' 
		BEGIN
			relationships=CNodeRelationships
		END
	)? 
;

// Some extra values for cross references

@Override
CNodePropertyValueCrossRef:
	CNodeCrossRefGetInput
;

CNodeCrossRefGetInput:
	"{{" "get_input:" input=[CInputVariable|CQualifiedName] "}}"
;

// Terminal & Custom types
// ---

@Override 
CValueExpression:
	CSTRING | CFLOAT | CBOOLEAN | CSIGNEDINT
;

@Override 
CSTRING:
	value = STRING
;

@Override 
CFLOAT: 
	value = FLOAT	
;

@Override 
CBOOLEAN: 
	value = BOOLEAN
;

@Override 
CSIGNEDINT:
	value = SIGNEDINT
;
