/**
 * DevSecOps Modelling Language
 *
 * Author: Tasio Mendez (Politecnico di Milano)
 * URL: https://github.com/tasiomendez/
 * Version: 1.0
 */
 
//grammar org.piacere.dsl.DOML with org.eclipse.xtext.common.Terminals
grammar org.piacere.dsl.DOML with org.piacere.dsl.RMDF

generate dOML "http://www.piacere.org/dsl/DOML"
import "http://www.piacere.org/dsl/RMDF" as rmdf

DOMLModel:
	(elements += AbstractElement)*
;

@Override 
AbstractElement:
	('module:'
		BEGIN 
			module=CModule
		END
	)? &
	(imports += CImports)* &   
	('input:'
		BEGIN 
			input=CInputVariables
		END
	)? &
	('node_templates:' 
		BEGIN
			nodes=CNodeTemplates
		END
	)
;

CModule:
	('type:' CQualifiedName) &
	('provider:' ID)
;

CInputVariables:
	(input+=CInputVariable)+
;

CInputVariable:
	name=CQualifiedName ':' 
	BEGIN
		data=CPropertyBody
	END
;

CNodeTemplates:
	(nodes+=CNodeTemplate)+
;

CNodeTemplate:
	name=ID ':'
	BEGIN
		(template+=CNode)+
	END
;

CNode:
	('type:' type=[rmdf::CNodeType|CQualifiedName]) &
	('properties:' 
		BEGIN
			(properties+=CNodeProperty)+
		END
	)? &
	('relationships:' 
		BEGIN
			relationships=CNodeRelationships
		END
	)? 
;

//CNodeProperties:
//	(properties+=CNodeProperty)+
//;

CNodeProperty:
	name=ID ':' value=(CValueExpression | CRefInputVariable)
;

CNodeRelationships:
	(relationships+=CNodeRelationship)+
;

CNodeRelationship:
	name=ID ':' value=[CNodeTemplate|ID]
;

CRefInputVariable:
	"{{" "get_input:" input=[CInputVariable|CQualifiedName] "}}"
;

