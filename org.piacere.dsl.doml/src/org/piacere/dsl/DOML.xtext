/**
 * DevSecOps Modelling Language
 *
 * Author: Tasio Mendez (Politecnico di Milano)
 * URL: https://github.com/tasiomendez/
 * Version: 1.0
 */
 
//grammar org.piacere.dsl.DOML with org.eclipse.xtext.common.Terminals
grammar org.piacere.dsl.DOML with org.piacere.dsl.RMDF

generate dOML "http://www.piacere.org/dsl/DOML"
import "http://www.piacere.org/dsl/RMDF" as rmdf

DOMLModel:
	(elements += AbstractElement)*
;

@Override 
AbstractElement:
	('metadata:'
		BEGIN 
			metadata=CMetadata
		END
	) &
	('input:'
		BEGIN 
			input=CInputVariables
		END
	)? &
	('node_templates:' 
		BEGIN
			nodes=CNodeTemplates
		END
	)
;

// METADATA Block
// ---

@Override 
CMetadata:
	('_name:' name=CQualifiedName)
	('_provider:' provider=ID) &
	('_version:' version=CSTRING)? &
	('_description:' description=CSTRING)?
;

// INPUT Variables Block

CInputVariables:
	(input+=CInputVariable)+
;

CInputVariable:
	name=CQualifiedName ':' 
	BEGIN
		data=CPropertyBody
	END
;

// Some extra values for cross references

@Override
CNodePropertyValueCrossRef:
	CNodeCrossRefGetInput
;

CNodeCrossRefGetInput:
	"{{" "get_input:" input=[CInputVariable|CQualifiedName] "}}"
;

// Terminal & Custom types
// ---

@Override 
CValueExpression:
	CSTRING | CFLOAT | CBOOLEAN | CSIGNEDINT
;

@Override 
CSTRING:
	value = STRING
;

@Override 
CFLOAT: 
	value = FLOAT	
;

@Override 
CBOOLEAN: 
	value = BOOLEAN
;

@Override 
CSIGNEDINT:
	value = SIGNEDINT
;
