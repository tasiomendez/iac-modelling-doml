/*
 * generated by Xtext 2.25.0
 */
package org.piacere.dsl.generator

import com.google.inject.Inject
import org.eclipse.emf.common.util.URI
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.EcoreUtil2
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.eclipse.xtext.resource.IEObjectDescription
import org.eclipse.xtext.resource.IResourceDescriptions
import org.piacere.dsl.dOML.CBOOLEAN
import org.piacere.dsl.dOML.CInputVariable
import org.piacere.dsl.dOML.CNodeCrossRefGetInput
import org.piacere.dsl.dOML.COutputVariable
import org.piacere.dsl.dOML.DOMLModel
import org.piacere.dsl.rMDF.CConcatValues
import org.piacere.dsl.rMDF.CFLOAT
import org.piacere.dsl.rMDF.CIntrinsicFunctions
import org.piacere.dsl.rMDF.CMetadata
import org.piacere.dsl.rMDF.CNode
import org.piacere.dsl.rMDF.CNodeCrossRefGetAttribute
import org.piacere.dsl.rMDF.CNodeCrossRefGetValue
import org.piacere.dsl.rMDF.CNodeNestedProperty
import org.piacere.dsl.rMDF.CNodeProperty
import org.piacere.dsl.rMDF.CNodePropertyValue
import org.piacere.dsl.rMDF.CNodePropertyValueInlineSingle
import org.piacere.dsl.rMDF.CNodeTemplate
import org.piacere.dsl.rMDF.CNodeType
import org.piacere.dsl.rMDF.CSIGNEDINT
import org.piacere.dsl.rMDF.CSTRING
import org.piacere.dsl.rMDF.CValueExpression
import org.piacere.dsl.rMDF.RMDFModel
import org.piacere.dsl.rMDF.RMDFPackage

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class DOMLGenerator extends AbstractGenerator {

	@Inject
	IResourceDescriptions descriptions;
	
	String author = "Tasio Mendez (Politecnico di Milano)"
	String email = "tasio.mendez@mail.polimi.it"

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val filename = this.getFilename(resource.URI)
		fsa.generateFile(filename, resource.compile)
	}

	// «»
	def compile(Resource resource) '''
		# Auto-generated file with PIACERE
		## Filename: «getFilename(resource.URI)» 
		## Author: «this.author»
		##         «this.email»
		
		tosca_definitions_version: cloudify_dsl_1_3
		
		«FOR m : resource.allContents.toIterable.filter(CMetadata)»
			«m.compile»
		«ENDFOR»	
		
		imports:
			- http://cloudify.co/spec/cloudify/4.5.5/types.yaml
			
		««« inputs:
		«FOR i : resource.allContents.toIterable.filter(CInputVariable) BEFORE 'inputs: \n'»
			
				«i.compile»
		«ENDFOR»
		
		«««	node_templates:
		«FOR n : resource.root.nodes.nodes BEFORE 'node_templates: \n'»
			
				«n.compile»
		«ENDFOR»
		
		««« outputs:
		«FOR i : resource.allContents.toIterable.filter(COutputVariable) BEFORE 'outputs: \n'»
			
				«i.compile»
		«ENDFOR»
		
	'''

	def compile(CMetadata metadata) '''
		description: >
			«metadata.description.value»
	'''

	def compile(CInputVariable variable) '''
		«variable.name»:
			«IF variable.data.type !== null»
				type: «this.trim(variable.data.type?.predefined.toLowerCase)»
			«ENDIF»
			«IF variable.data.description !== null»
				description: «this.trim(variable.data.description?.value)»
			«ENDIF»
			«IF variable.data.^default !== null»
				default: «this.getValueExpr(variable.data.^default, false)»
			«ENDIF»
	'''

	def compile(CNodeTemplate node) '''
		«IF node.template.type.data.nodes !== null»
			«FOR n : node.template.type.data.nodes.nodes»
				«n.compile»
			«ENDFOR»
		«ELSE»
			«node.name»:
				«node.template.compile»
				
		«ENDIF»
	'''

	def compile(CNode node) '''
		type: «this.trim(node.type.name)»
		properties:
			«FOR p : node.properties»
				«p.compile»
			«ENDFOR»
	'''

	def compile(CNodeProperty property) '''
		«property.name.name»: 
	'''

	def compile(CNodeNestedProperty property) '''
		«FOR p : property.properties»
			«p.compile»
		«ENDFOR»
	'''

	def compile(COutputVariable variable) '''
		«variable.name»:
			«IF variable.value !== null»
				value: «this.getValueExprInline(variable.value as CNodePropertyValueInlineSingle)»
			«ENDIF»
	'''

	def getValueProperty(CNodePropertyValue value) {
		switch value {
			CNodeNestedProperty: value.compile
			CNodePropertyValueInlineSingle: this.getValueExprInline(value)
		}
	}

	def getValueExpr(CValueExpression expr, Boolean quotes) {
		switch expr {
			CSTRING case quotes: '"' + expr.value + '"'
			CSTRING: expr.value
			CFLOAT case quotes: '"' + expr.value + '"'
			CFLOAT: expr.value
			CBOOLEAN case quotes: '"' + expr.value + '"'
			CBOOLEAN: expr.value
			CSIGNEDINT case quotes: '"' + expr.value + '"'
			CSIGNEDINT: expr.value
			default: ''
		}
	}

	def getValueExprInline(CNodePropertyValueInlineSingle expr) {
		switch expr {
			CValueExpression: this.getValueExpr(expr, true)
			CIntrinsicFunctions: this.getIntrinsicFunction(expr)
			default: ''
		}
	}

	def getIntrinsicFunction(CIntrinsicFunctions func) {
		switch func {
			CConcatValues: func.compile
			CNodeCrossRefGetInput: func.compile
			CNodeCrossRefGetAttribute: func.compile
			CNodeCrossRefGetValue: func.compile
		}
	}

	def compile(CConcatValues expr) '''
		{ concat: [ 
					«this.getValueExprInline(expr.first)»«FOR i : expr.list BEFORE ',' SEPARATOR ','»
						«this.getValueExprInline(i as CNodePropertyValueInlineSingle)»«ENDFOR» ] }
	'''

	def compile(CNodeCrossRefGetInput expr) '''
		{ get_input: «expr.input.name» }
	'''

	def compile(CNodeCrossRefGetAttribute expr) '''
		{ get_attr: [ «expr.node.name», «expr.attr» ] }
	'''

	def compile(CNodeCrossRefGetValue expr) '''
		"PENDING TO IMPLEMENT"
	'''

	def getRoot(Resource r) {
		EcoreUtil2.getRootContainer(r.allContents.toIterable.get(0)) as DOMLModel
	}

	def getProviderImplementations(CNodeType node) {
		val exported = descriptions.getExportedObjectsByType(RMDFPackage.Literals.CNODE_TYPE)
		return exported.map [ IEObjectDescription t |
			EcoreUtil2.resolve(t.getEObjectOrProxy(), node) as CNodeType
		].filter [ CNodeType n |
			n.data?.superType?.name === node.name
		].toMap([ CNodeType n |
			this.getProvider(n)
		])
	}

	def getProvider(EObject obj) {
		val root = EcoreUtil2.getRootContainer(obj)
		return switch root {
			RMDFModel: root.metadata.provider
			DOMLModel: root.metadata.provider
		}
	}

	def getFilename(URI uri) {
		var filename = uri.toString
		filename = filename.replace("platform:/resource", "")
		filename = filename.substring(filename.indexOf('/', 1) + 1).replaceFirst('/', ".") + ".yml";
		return filename
	}

	def trim(String value) {
		return value.trim
	}
}
